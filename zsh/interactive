#!/bin/zsh

# force 256-color mode when inside containers
if [ -d /boot ] && [ -n "$(find /boot -maxdepth 0 -empty)" ]; then
  export TERM=screen-256color
fi

# ls aliases
alias ls="ls --color=auto"
alias ll="ls -lh"
alias la="ls -a"
alias lla="ll -a"

# color aliases
alias diff="diff --color=auto"
alias grep="grep --color=auto"
alias ip="ip --color=auto"
alias dmesg="dmesg --color=auto"

# eza
if [[ $commands[eza] ]]; then
    alias ls=eza
    alias lt='ll --tree'
fi

# bat
if [[ $commands[bat] ]]; then
    export BAT_STYLE="snip"
    export BAT_THEME="base16"
    export BAT_THEME_DARK="base16"
    export BAT_THEME_LIGHT="base16"
    alias cat=bat
fi

# zoxide
if [[ $commands[zoxide] ]]; then
    eval "$(zoxide init zsh)"
fi

# neovim
if [[ $commands[nvim] ]]; then
    export EDITOR=nvim
elif [[ $commands[vim] ]]; then
    export EDITOR=vim
fi

# keybindings
source "$ZDOTDIR/keybindings"

# less
export LESS=" \
    --quit-if-one-screen \
    --ignore-case \
    --status-column \
    --LONG-PROMPT \
    --RAW-CONTROL-CHARS \
    --HILITE-UNREAD \
    --tabs=4 \
    --no-init \
    --window=-4 \
"
# a couple of keybindings and color config for less
# requires a fairly recent version of less
export LESSKEYIN="$HOME/.dotfiles/.lesskey"

# zinit
export ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
if [[ ! -d "$ZINIT_HOME" ]]; then
    ZINIT_HOME="$(dirname "$ZINIT_HOME")" \
    NO_EMOJI=1 \
    NO_INPUT=1 \
    NO_TUTORIAL=1 \
    NO_ANNEXES=1 \
    NO_EDIT=1 \
    sh -c "$(curl -fsSL https://git.io/zinit-install)"
fi

source "$ZINIT_HOME/zinit.zsh"

# powerlevel10k theme
zinit ice depth=1
zinit light romkatv/powerlevel10k

# fast syntax highlighting
zinit ice wait=!0 lucid depth=1
zinit light zdharma-continuum/fast-syntax-highlighting

# fish-like abbreviations, aliases that auto-expand when pressing space
init_abbr() {
    ABBR_USER_ABBREVIATIONS_FILE="$ZDOTDIR/abbreviations"
    ABBR_AUTOLOAD=0
    ABBR_DEFAULT_BINDINGS=0
    bindkey " " abbr-expand-and-insert # space to expand abbr
    bindkey "^ " magic-space # ctrl+space to insert space without expanding abbr
    bindkey -M isearch " " magic-space # normal space is normal in incremental search
    bindkey -M isearch "^ " abbr-expand-and-insert # ctrl+space expands abbr in isearch

    # Allows abbreviation to be expanded on enter if they're the only command to execute
    _expand_abbr_and_accept() {
        emulate -LR zsh

        # make sure the rbuffer doesn't start with a space
        if [[ ! "$RBUFFER" || "$RBUFFER" =~ ^[[:space:]].+ ]]; then
            abbr-expand-and-accept
        else
            zle accept-line
        fi
    }
    zle -N _expand_abbr_and_accept
    bindkey "^M" _expand_abbr_and_accept
}
zinit ice wait=0 lucid depth=1 atinit=init_abbr
zinit light olets/zsh-abbr

# fish like substring search
init_substring_search() {
    HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_FOUND=underline
    HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_NOT_FOUND=fg=8,underline
    HISTORY_SUBSTRING_SEARCH_FUZZY=1
    bindkey '^[[A' history-substring-search-up # up
    bindkey '^[OA' history-substring-search-up
    bindkey '^[[B' history-substring-search-down # down
    bindkey '^[OB' history-substring-search-down
}
zinit ice wait=0 lucid depth=1 atinit=init_substring_search
zinit light zsh-users/zsh-history-substring-search

# calculate fzf preview window params based on terminal size
preview_params() {
    # sets fzf preview position and status (no/hidden) depending on terminal size
    local hide_threshold_columns=74
    local hide_threshold_lines=36
    local lines_factor=2.3 # assume that a cell is n times a tall as it wide

    local config="default"
    local hidden="nohidden"

    if ((COLUMNS > LINES * lines_factor)); then   # screen is wider than it is tall
        config="right,50%,border-left"

        # force hide the preview window if the screen is not wide enough
        if ((COLUMNS < hide_threshold_columns)); then
            hidden="hidden"
        fi
    else # screen is taller than it is wide
        config="bottom,50%,border-top"

        # force hide the preview window if the screen is not tall enough
        if ((LINES < hide_threshold_lines)); then
            hidden="hidden"
        fi
    fi

    echo "$config,$hidden"
}

# forgit
init_forgit() {
    export FORGIT_NO_ALIASES=1

    set_fzf_opts() {
        export FORGIT_FZF_DEFAULT_OPTS="--preview-window=$(preview_params)"
    }

    trap set_fzf_opts SIGWINCH # call whenever the terminal resizes
    set_fzf_opts
}
zinit ice wait=0 lucid depth=1 atload='path=($PWD/bin $path)' atinit=init_forgit
zinit light wfxr/forgit

# fzf
if [[ $commands[fzf] ]]; then
    set_fzf_opts() {
        preview_window=$(preview_params)
        export FZF_DEFAULT_OPTS="
            --ansi
            --cycle
            --reverse
            --info=inline-right
            --no-separator
            --height=30%
            --border=none
            --list-border=none
            --input-border=none
            --preview='preview {}'
            --pointer=î­°
            --highlight-line
            --color=gutter:-1,pointer:255,scrollbar:4,preview-scrollbar:4
            --bind='ctrl-space:toggle-down'
            --bind='ctrl-z:toggle-preview'
            --bind='ctrl-y:execute-silent(printf '%s' {+} | zsh -ic to-clip)'
            --bind='ctrl-u:preview-up+preview-up+preview-up+preview-up+preview-up'
            --bind='ctrl-d:preview-down+preview-down+preview-down+preview-down+preview-down'
            --bind='ctrl-r:toggle-all'
            --bind='ctrl-s:toggle-sort'
            --bind='ctrl-w:toggle-preview-wrap'
            --bind='home:first'
            --bind='end:last'
            --preview-window=$preview_window
        "
        export FZF_CTRL_T_OPTS="--no-height --no-border --preview-window=$preview_window"
        export FZF_ALT_C_OPTS="--no-height --no-border --preview-window=$preview_window"
    }

    export FZF_DEFAULT_COMMAND="fd --type f --type l --color=never --no-ignore-vcs --hidden"
    export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
    export FZF_ALT_C_COMMAND="fd --type d --color=never --no-ignore-vcs --hidden"

    trap set_fzf_opts SIGWINCH # call whenever the terminal resizes
    set_fzf_opts

    _fzf_complete_ssht() {
        _fzf_complete --prompt="ssh> " -- "$@" < <(
            cat ~/.ssh/config | grep -E '^Host\s+' | awk '{print $2}'
        )
    }

    eval "$(fzf --zsh)"
fi

# Enable automatic activation of virtualenvs
if [[ $commands[pyenv] ]]; then
    eval "$(pyenv init --path)"
    eval "$(pyenv init -)"
    eval "$(pyenv virtualenv-init -)"
fi
